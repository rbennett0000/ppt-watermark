Option Explicit

Sub ConvertPPTtoPDFwithWatermark()

    Dim objPresentation As Presentation
    Dim shpWatermark As shape
    Dim slideIndex As Long
    
    Dim excelApp As Object
    Dim excelWorkbook As Object
    Dim excelWorksheet As Object
    Dim rowIndex As Long
    Dim lastRow As Long
    Dim startRow As Long
    
    Dim pdfFile As String
    Dim companyName As String
    
    Dim coll As New Collection
    Dim item As Object
    
    ' Set constants
    Const EXCEL_FILE_PATH As String = "C:\bin\attendees2.xlsx"
    Const PDF_FILE_PATH As String = "C:\bin\"
    Const PLACEHOLDER_TEXT As String = "Lorem ipsum dolor sit amet, consectetur adipiscing elit, sed do eiusmod tempor incididunt ut labore et dolore magna aliqua."
    startRow = 3
    lastRow = 3
    
    ' Create watermark shape on first slide
    On Error GoTo Cleanup
    Set objPresentation = ActivePresentation
    Set shpWatermark = objPresentation.Slides(1).Shapes.AddTextbox(Orientation:=msoTextOrientationHorizontal, Left:=-200, Top:=75, Width:=2000, Height:=50)
    shpWatermark.TextFrame.textRange.Text = PLACEHOLDER_TEXT
    shpWatermark.IncrementRotation (-30)
        
    With shpWatermark.TextFrame.textRange.Font
        .Size = 32
        .Name = "Segoe UI Semibold"
        .Color.RGB = RGB(89, 89, 89)
    End With
        
    With shpWatermark.TextFrame2.textRange.Font
        .Line.Visible = True
        .Line.ForeColor.RGB = RGB(255, 255, 255)
        .Line.Transparency = 0.56
        .Fill.Transparency = 0.84
    End With
            
    coll.Add shpWatermark
    
    ' Copy watermark shape and paste on each slide
    shpWatermark.Copy
    For slideIndex = 2 To objPresentation.Slides.Count
        coll.Add objPresentation.Slides(slideIndex).Shapes.Paste
    Next slideIndex

    ' Loop through Excel rows and replace watermark text with company name
    Set excelApp = CreateObject("Excel.Application")
    Set excelWorkbook = excelApp.Workbooks.Open(EXCEL_FILE_PATH)
    Set excelWorksheet = excelWorkbook.Sheets("tblCompanies")
    
    For rowIndex = startRow To lastRow
        companyName = excelWorksheet.Cells(rowIndex, 1).Value ' Assuming the company name is in column A
        
        ' Iterate through all the watermark shapes and change the text
        For Each item In coll
            item.TextFrame.textRange.Text = "Prepared for " & companyName & " -  Prepared for " & companyName & " - Prepared for " & companyName & " - Prepared for " & companyName & " - Prepared for " & companyName
        Next item
           
        ' Export PDF version
        pdfFile = PDF_FILE_PATH & "Teams_AI_Summit_" & companyName & ".pdf"
        objPresentation.ExportAsFixedFormat pdfFile, ppFixedFormatTypePDF
    Next rowIndex
    
Cleanup:
    ' Clean up all the watermark shapes and message result
    For Each item In coll
        item.Delete
    Next item
    Set coll = Nothing

    ' Clean up objects
    If Not excelWorksheet Is Nothing Then
        excelWorksheet.Close False
        Set excelWorksheet = Nothing
    End If

    If Not excelWorkbook Is Nothing Then
        excelWorkbook.Close False
        Set excelWorkbook = Nothing
    End If

    If Not excelApp Is Nothing Then
        excelApp.Quit
        Set excelApp = Nothing
    End If

    Set objPresentation = Nothing
    Set shpWatermark = Nothing 

    If Err.Number = 0 Then
        MsgBox "Watermarking and PDF conversion complete!", vbInformation
    Else
        MsgBox "Operation failed - error number " & Str(Err.Number), vbInformation
    End If

End Sub

Sub removeWatermarks()

Dim pres As Presentation
Dim slido As slide
Dim shapo As shape

Set pres = ActivePresentation
    For Each slido In pres.Slides
        For Each shapo In slido.Shapes
            If shapo.HasTextFrame = True Then
                If shapo.TextFrame.textRange.Text = "Lorem ipsum dolor sit amet, consectetur adipiscing elit, sed do eiusmod tempor incididunt ut labore et dolore magna aliqua." Then
                    shapo.Delete
                End If
            End If
        Next shapo
     Next slido
End Sub

